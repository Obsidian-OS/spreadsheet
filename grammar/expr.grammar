@detectDelim
@top Formula { expression }

@precedence {
    eq @left,
    bin @left,
    comp @left,
    add @left,
    mul @left,
    exp @left
}

expression {
    literal |
    Access |
    Call |
    operation |
    List |
    AssociativeArray |
    bracketedExpression
}

literal[@isGroup=Literal] {
    Number |
    Boolean |
    key |
    Address |
    NamedColumn
}

key { Name | String }

Access { ~key (literal | bracketedExpression) "." key }

Call { ~key (literal | bracketedExpression) "(" delimited<expression> ")" }

operation {
    expression !eq ("==" | "!=") expression |
    expression !bin ("&&" | "||" | "!") expression |
    expression !comp ("<" | ">") expression |
    expression !add ("+" | "-") expression |
    expression !mul ("*" | "/" | "%") expression |
    expression !exp ("^") expression
}

List { "[" ~obj delimited<expression> "]" }

AssociativeArray { "[" ~obj delimited<(key "=" expression)> "]" }

delimited<body> { ( body ("," body)* )? }

bracketedExpression { "(" expression ")" }

@local tokens {
    stringEnd { '"' }
    stringEscape { '\\' ![u] }
    stringUnicode { '\\u{' ![}]* '}' }

    @else stringContents
}

@skip {} {
    String { '"' (stringEscape | stringUnicode | stringContents)* stringEnd }
}

@local tokens {
    addressEnd { '}' }

    @else addressContents
}

@skip {} {
    Address { ~col '{' addressContents* addressEnd }
}

@local tokens {
    namedColumnEnd { '}}' }

    @else namedColumnContents
}

@skip {} {
    NamedColumn { ~col '{{' namedColumnContents* namedColumnEnd }
}

@tokens {

    alpha { $[_\u{a1}-\u{10ffff}] }
    alphanumeric { $[_\u{a1}-\u{10ffff}\d] }

    Boolean {
        "true" | "false"
    }

    Name {
        alpha alphanumeric*
    }

    Number {
        ("-"? $[0-9]+ ) |
        ("-"? "0x" $[0-9a-fA-Z_]+) |
        ("-"? "0o" $[0-7]+) |
        ("-"? "0b" $[01]+) |
        ("-"? $[0-9]+ "." $[0-9]*)
    }
}